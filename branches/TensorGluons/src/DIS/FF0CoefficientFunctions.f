************************************************************************
*
*     FFCoefficientFunctions.f:
*
*     This file contains all the exact asymptotic FF coefficient functions
*     as given in the paper written below.
*
************************************************************************

C Reference: hep-ph/9601302 (Appendix D)

C Equation (4.3): Gluon coefficient function for FL
C -- Term proportional to ln(mu^2/m^2)
      DOUBLE PRECISION FUNCTION CLGM(X,Q2,FMU2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER (TF=0.5D0)
      PARAMETER (CA=3D0)
      DLX=DLOG(X)
      DLM=DLOG(1.0D0-X)
      DLF=DLOG(HM2/FMU2)
      A1=64.0D0*X*(1.0D0-X)*DLM-128.0D0*X*DLX-32.0D0-160.0D0*X
     1+544.0D0*X*X/3.0D0+32.0D0/X/3.0D0
      CLGM=CA*TF*A1*DLF
      RETURN
      END
C -- constant terms and proportional to ln(Q^2/m^2) times CA
      DOUBLE PRECISION FUNCTION CLGCA(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CA=3D0)
      ZETA2=1.6449340668D0
      S11=WGPLG(1,1,1.0D0-X)
      S11M=WGPLG(1,1,-X)
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLM=DLOG(1.0D0-X)
      DLM2=DLM*DLM
      DLP=DLOG(1.0D0+X)
      DL=DLOG(Q2/HM2)
      A1=64.0D0*X*(1.0D0-X)*DLM-128.0D0*X*DLX-32.0D0-160.0D0*X
     1+544.0D0*X*X/3.0D0+32.0D0/X/3.0D0
      A2=96.0D0*X*DLX2+(64.0D0*X*X-192.0D0*X)*DLX*DLM+(32.0D0
     1-416.0D0*X*X+256.0D0*X)*DLX+32.0D0*X*(1.0D0-X)*DLM2+(
     2-32.0D0+928.0D0*X*X/3.0D0-288.0D0*X+32.0D0/X/3.0D0)*DLM
     3+64.0D0*X*X*ZETA2-128.0D0*X*S11+64.0D0*X*(1.0D0+X)*(S11M
     4+DLX*DLP)+32.0D0/3.0D0-1696*X*X/9.0D0+544.0D0*X/3.0D0
     5-32.0D0/X/9.0D0
      CLGCA =CA*TF*(A1*DL+A2)
      RETURN
      END
C -- constant terms and proportional to ln(Q^2/m^2) times CF
      DOUBLE PRECISION FUNCTION CLGCF(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      ZETA2=1.6449340668D0
      S11=WGPLG(1,1,1.0D0-X)
      S11M=WGPLG(1,1,-X)
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLM=DLOG(1.0D0-X)
      DLM2=DLM*DLM
      DLP=DLOG(1.0D0+X)
      DL=DLOG(Q2/HM2)
      B1=32.0D0*X*DLX+16.0D0*(1.0D0-2.0D0*X*X+X)
      B2=-(64.0D0*X*X*X/5.0D0+64.0D0*X/3.0D0)*DLX2+32.0D0*X
     1*(S11+DLX*DLM)+(-208.0D0/15.0D0+192.0D0*X*X/5.0D0
     2-416.0D0*X/5.0D0-64.0D0/X/15.0D0)*DLX+(16.0D0-64.0D0*X*X
     3+48.0D0*X)*DLM+(128.0D0*X*X*X/5.0D0-64.0D0*X/3.0D0)*ZETA2
     4+(128.0D0*X*X*X/5.0D0-64.0D0*X/3.0D0+64.0D0/X/X/15.0D0)
     5*(S11M+DLX*DLP)-256.0D0/15.0D0+672.0D0*X*X/5.0D0-608.0D0
     6*X/5.0D0+64.0D0/X/15.0D0
      CLGCF=CF*TF*(B1*DL+B2)
      RETURN
      END

C Equation (4.4): Gluon coefficient function for F2
C -- Term proportional to ln(mu^2/m^2)
      DOUBLE PRECISION FUNCTION C2GM(X,Q2,FMU2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CA=3D0)
      ZETA2=1.6449340668D0
      S11=WGPLG(1,1,1.0D0-X)
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLM=DLOG(1.0D0-X)
      DLM2=DLM*DLM
      DL=DLOG(Q2/HM2)
      DLF=DLOG(HM2/FMU2)
      A1=-248.0D0*X*X/3.0D0+64.0D0*X+8.0D0+32.0D0/X/3.0D0
     1+(32.0D0*X*X-32.0D0*X+16.0D0)*DLM+(64.0D0*X+16.0D0)*DLX
      A2=(-16.0D0+32.0D0*X-32.0D0*X*X)*ZETA2+1124.0D0*X*X/3.0D0
     1-968.0D0*X/3.0D0-172.0D0/3.0D0+16.0D0/X/3.0D0+(16.0D0
     2-32.0D0*X+32.0D0*X*X)*DLM2-(8.0D0+32.0D0*X)*DLX2+(248.0D0
     3*X*X/3.0D0-256.0D0*X-8.0D0)*DLX+(32.0D0/X/3.0D0-8.0D0
     4+192.0D0*X-632.0D0*X*X/3.0D0)*DLM+(96.0D0*X-32.0D0*X*X)
     5*DLX*DLM+(64.0D0*X+16.0D0)*S11
      C2GM=CA*TF*(A1*DL+A2)*DLF
      END
C -- constant terms and proportional to ln(Q^2/m^2) times CA
      DOUBLE PRECISION FUNCTION C2GCA(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CA=3D0)
      S121MX=WGPLG(1,2,1.0D0-X)
      S12MX=WGPLG(1,2,-X)
      S211MX=WGPLG(2,1,1.0D0-X)
      S21MX=WGPLG(2,1,-X)
      Z=(1.0D0-X)/(1.0D0+X)
      S21Z=WGPLG(2,1,Z)
      S21MZ=WGPLG(2,1,-Z)
      S111MX=WGPLG(1,1,1.0D0-X)
      S11MX=WGPLG(1,1,-X)
      ZETA2=1.6449340668D0
      ZETA3=1.2020569031D0
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLX3=DLX2*DLX
      DLM=DLOG(1.0D0-X)
      DLM2=DLM*DLM
      DLM3=DLM2*DLM
      DLP=DLOG(1.0D0+X)
      DLP2=DLP*DLP
      DL=DLOG(Q2/HM2)
      DL2=DL*DL
      A1=16.0D0/X/3.0D0-124.0D0*X*X/3.0D0+32.0D0*X+4.0D0
     1+(16.0D0*X*X-16.0D0*X+8.0D0)*DLM+(32.0D0*X+8.0D0)*DLX
      A2=-(16.0D0+32.0D0*X*X)*ZETA2+1628.0D0*X*X/9.0D0
     1-368.0D0*X/3.0D0-220.0D0/3.0D0+208.0D0/X/9.0D0
     2+(16.0D0*X*X-16.0D0*X+8.0D0)*DLM2-(48.0D0*X+16.0D0)
     3*DLX2+(-536.0D0*X*X/3.0D0+160.0D0*X-8.0D0+32.0D0/X/3.0D0)
     4*DLM+(200.0D0*X*X-192.0D0*X)*DLX+(96.0D0*X-32.0D0*X*X)
     5*DLX*DLM+(64.0D0*X+16.0D0)*S111MX-(32.0D0*X*X+32.0D0*X+16.0D0)
     6*(S11MX+DLX*DLP)
      A31=DLX3*(16.0D0/3.0D0+16.0D0*X)+
     1DLX2*DLM*(-8.0D0+16.0D0*X*X-64.0D0*X)+
     2DLX2*DLP*(12.0D0+32.0D0*X*X+24.0D0*X)+
     3DLX2*(-114.0D0*X*X+184.0D0*X)+
     4DLX*DLM2*(-16.0D0*X*X+48.0D0*X)+
     5DLX*DLM*DLP*(-16.0D0-32.0D0*X*X-32.0D0*X)+
     6DLX*DLM*(16.0D0+292.0D0*X*X-288.0D0*X)+
     7DLX*DLP2*(8.0D0+16.0D0*X)
      A32=DLX*DLP*(-48.0D0+208.0D0*X*X/3.0D0+16.0D0*X-
     132.0D0/X/3.0D0)+
     2DLX*ZETA2*(-16.0D0+32.0D0*X*X-160.0D0*X)+
     3DLX*S111MX*(+32.0D0*X)+
     4DLX*S11MX*(24.0D0+32.0D0*X*X+48.0D0*X)+
     5DLX*(292.0D0/3.0D0-5780.0D0*X*X/9.0D0+332.0D0*X)+
     6DLM2*(-6.0D0-214.0D0*X*X/3.0D0+64.0D0*X+16.0D0/X/3.0D0)+
     7ZETA2*DLM*(-40.0D0-64.0D0*X*X+48.0D0*X)
      A33=DLM*S111MX*(16.0D0+64.0D0*X)+
     1DLM*S11MX*(-16.0D0-32.0D0*X*X-32.0D0*X)+
     2DLM*(-112.0D0/3.0D0+2996.0D0*X*X/9.0D0-860.0D0*X/3.0D0
     3+208.0D0/X/9.0D0)+
     4ZETA2*DLP*(8.0D0+16.0D0*X)+
     5DLP*S11MX*(16.0D0+32.0D0*X)+
     6S21MZ*(-16.0D0-32.0D0*X*X-32.0D0*X)+
     7S21Z*(16.0D0+32.0D0*X*X+32.0D0*X)
      A34=ZETA2*(-4.0D0+796.0D0*X*X/3.0D0-208.0D0*X-32.0D0/X)+
     1ZETA3*(-12.0D0-8.0D0*X*X-56.0D0*X)+
     2S111MX*(20.0D0+80.0D0*X*X/3.0D0-64.0D0*X+64.0D0/X/3.0D0)+
     3S211MX*(-16.0D0-128.0D0*X)+
     4S121MX*(40.0D0+144.0D0*X)+
     5S11MX*(-48.0D0+208.0D0*X*X/3.0D0+16.0D0*X-32.0D0/X/3.0D0)+
     6S21MX*(-24.0D0-48.0D0*X)+
     7S12MX*(16.0D0+32.0D0*X)+80.0D0/X/9.0D0+
     8466.0D0/9.0D0-878.0D0*X*X/9.0D0+260.0D0*X/9.0D0
      A3=A31+A32+A33+A34
      C2GCA=CA*TF*(A1*DL2+A2*DL+A3)
      RETURN
      END
C -- constant terms and proportional to ln(Q^2/m^2) times CF
      DOUBLE PRECISION FUNCTION C2GCF(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      S121MX=WGPLG(1,2,1.0D0-X)
      S12MX=WGPLG(1,2,-X)
      S211MX=WGPLG(2,1,1.0D0-X)
      S21MX=WGPLG(2,1,-X)
      Z=(1.0D0-X)/(1.0D0+X)
      S21Z=WGPLG(2,1,Z)
      S21MZ=WGPLG(2,1,-Z)
      S111MX=WGPLG(1,1,1.0D0-X)
      S11MX=WGPLG(1,1,-X)
      ZETA2=1.6449340668D0
      ZETA3=1.2020569031D0
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLX3=DLX2*DLX
      DLM=DLOG(1.0D0-X)
      DLM2=DLM*DLM
      DLM3=DLM2*DLM
      DLP=DLOG(1.0D0+X)
      DLP2=DLP*DLP
      DL=DLOG(Q2/HM2)
      DL2=DL*DL
      B1=-2.0D0+8.0D0*X+(16.0D0*X*X-16.0D0*X+8.0D0)*DLM
     1+(-16.0D0*X*X+8.0D0*X-4.0D0)*DLX
      B2=(-64.0D0*X*X+64.0D0*X-32.0D0)*ZETA2+16.0D0*X*X-68.0D0*X
     1+36.0D0+(32.0D0*X*X-32.0D0*X+16.0D0)*DLM2+(32.0D0*X*X
     2-16.0D0*X+8.0D0)*DLX2+(-80.0D0*X*X+96.0D0*X-28.0D0)*DLM
     3+(80.0D0*X*X-48.0D0*X+8.0D0)*DLX+(-64.0D0*X*X+48.0D0*X
     4-24.0D0)*DLX*DLM+(8.0D0-16.0D0*X)*S111MX
      B31=DLX3*(-8.0D0/3.0D0-32.0D0*X*X/3.0D0+16.0D0*X/3.0D0)+
     1DLX2*DLM*(16.0D0+48.0D0*X*X-32.0D0*X)+
     2DLX2*DLP*(16.0D0+16.0D0*X*X+32.0D0*X)+
     3DLX2*(-4.0D0-96.0D0*X*X*X/5.0D0-52.0D0*X*X+8.0D0*X/3.0D0)+
     4DLX*DLM2*(-20.0D0-48.0D0*X*X+40.0D0*X)+
     5DLX*DLM*(24.0D0+168.0D0*X*X-160.0D0*X)+
     6DLX*DLP2*(-32.0D0-32.0D0*X*X-64.0D0*X)+
     7DLX*DLP*(96.0D0+192.0D0*X*X*X/5.0D0+128.0D0*X/3.0D0)
      B32=16.0D0*DLX*DLP/X/X/15.0D0-16.0D0*DLX/X/15.0D0+
     1DLX*ZETA2*(32.0D0+64.0D0*X*X-64.0D0*X)+
     2DLX*S111MX*(32.0D0*X*X)+
     3DLX*S11MX*(-32.0D0-32.0D0*X*X+64.0D0*X)+
     4DLX*(-712.0D0/15.0D0-672.0D0*X*X/5.0D0+136.0D0*X/5.0D0)+
     5DLM3*(8.0D0+16.0D0*X*X-16.0D0*X)+
     6DLM2*(-22.0D0-84.0D0*X*X+88.0D0*X)+
     7ZETA2*DLM*(-32.0D0*X*X)
      B33=DLM*S111MX*(8.0D0-16.0D0*X)+
     1DLM*(28.0D0+96.0D0*X*X-132.0D0*X)+
     2ZETA2*DLP*(-32.0D0-32.0D0*X*X-64.0D0*X)+
     3DLP*S11MX*(-64.0D0-64.0D0*X*X-128.0D0*X)+
     4ZETA2*(48.0D0+192.0D0*X*X*X/5.0D0+104.0D0*X*X-
     5208.0D0*X/3.0D0)+
     6ZETA3*(112.0D0+192.0D0*X*X-96.0D0*X)
      B34=S111MX*(-24.0D0+64.0D0*X*X-48.0D0*X)+
     1S211MX*(-24.0D0-32.0D0*X*X+48.0D0*X)+
     2S121MX*(-32.0D0+64.0D0*X)+
     3S11MX*(96.0D0+192.0D0*X*X*X/5.0D0+128.0D0*X/3.0D0)+
     416.0D0*S11MX/X/X/15.0D0+16.0D0/X/15.0D0+
     5S21MX*(96.0D0+96.0D0*X*X-64.0D0*X)+
     6S12MX*(-64.0D0-64.0D0*X*X-128.0D0*X)-
     7904.0D0/15.0D0+328.0D0*X*X/5.0D0+68.0D0*X/5.0D0
      B3=B31+B32+B33+B34
      C2GCF=CF*TF*(B1*DL2+B2*DL+B3)
      RETURN
      END

C Equation (4.5): Heavy quark coefficient function for FL
C -- Term proportional to ln(mu^2/m^2)
      DOUBLE PRECISION FUNCTION CLQ1M(X,Q2,FMU2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      DLX=DLOG(X)
      DLF=DLOG(HM2/FMU2)
      CLQ1M=CF*TF*(32.0D0/3.0D0/X+64.0D0*X*X/3.0D0
     1-32.0D0-32.0D0*X*DLX)*DLF
      RETURN
      END
C -- constant terms and proportional to ln(Q^2/m^2)
      DOUBLE PRECISION FUNCTION CLQ1(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      SPX=WGPLG(1,1,1.0D0-X)
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLM=DLOG(1.0D0-X)
      DL=DLOG(Q2/HM2)
      A1=-32.0D0*X*DLX-32.0D0+64.0D0*X*X/3.0D0+32.0D0/X/3.0D0
      A2=32.0D0*X*(DLX2-DLX*DLM-SPX)+(-32.0D0+64.0D0*X*X/3.0D0
     1+32.0D0/X/3.0D0)*DLM+(32.0D0-64.0D0*X*X-32.0D0*X)*DLX
     2+32.0D0/3.0D0+320.0D0*X*X/9.0D0-128.0D0*X/3.0D0
     3-32.0D0/X/9.0D0
      CLQ1=CF*TF*(A1*DL+A2)
      RETURN
      END

C Equation (4.6): Heavy quark coefficient function for F2
C -- Term proportional to ln(mu^2/m^2)
      DOUBLE PRECISION FUNCTION C2Q1M(X,Q2,FMU2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      SPX=WGPLG(1,1,1.0D0-X)
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLM=DLOG(1.0D0-X)
      DL=DLOG(Q2/HM2)
      DLF=DLOG(HM2/FMU2)
      A1=-32.0D0*X*X/3.0D0-8.0D0*X+8.0D0+32.0D0/X/3.0D0
     1+16.0D0*(1.0D0+X)*DLX
      A2=128.0D0*X*X/3.0D0+16.0D0*X/3.0D0-160.0D0/3.0D0
     1+16.0D0/X/3.0D0+8.0D0*(1.0D0+X)*(-DLX2+2.0D0*DLX*DLM
     2+2.0D0*SPX)+(-32.0D0*X*X/3.0D0-8.0D0*X+8.0D0+32.0D0/X/3.0D0)
     3*DLM+(32.0D0*X*X/3.0D0-40.0D0*X-8.0D0)*DLX
      C2Q1M=CF*TF*(A1*DL+A2)*DLF
      RETURN
      END
C -- constant terms and proportional to ln(Q^2/m^2)
      DOUBLE PRECISION FUNCTION C2Q1(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      ZETA2=1.6449340668D0
      S11=WGPLG(1,1,1.0D0-X)
      S12=WGPLG(1,2,1.0D0-X)
      S21=WGPLG(2,1,1.0D0-X)
      S11M=WGPLG(1,1,-X)
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLX3=DLX*DLX2
      DLM=DLOG(1.0D0-X)
      DLM2=DLM*DLM
      DLP=DLOG(1.0D0+X)
      DL=DLOG(Q2/HM2)
      DL2=DL*DL
      A1=-16.0D0*X*X/3.0D0-4.0D0*X+4.0D0+16.0D0/X/3.0D0
     1+8.0D0*(1.0D0+X)*DLX
      A2=-64.0D0*X*X/9.0D0+160.0D0*X/3.0D0-208.0D0/3.0D0
     1+208.0D0/X/9.0D0+32.0D0*X*X*DLX+16.0D0*(1.0D0+X)*(-DLX2+DLX*DLM
     2+S11)+(-32.0D0*X*X/3.0D0-8.0D0*X+8.0D0+32.0D0/X/3.0D0)
     3*DLM
      A3=(1.0D0+X)*(16.0D0*DLX3/3.0D0-16.0D0*DLX2*DLM+8.0D0
     1*DLX*DLM2-32.0D0*ZETA2*DLX+16.0D0*DLM*S11+32.0D0*S12
     2-16.0D0*S21)+(40.0D0*X-16.0D0*X*X)*DLX2+32.0D0*X*X*DLX
     3*DLM+(280.0D0/3.0D0-704.0D0*X*X/9.0D0-88.0D0*X)*DLX
     4+(4.0D0-16.0D0*X*X/3.0D0-4.0D0*X+16.0D0/X/3.0D0)*DLM2
     5+(-208.0D0/3.0D0-64.0D0*X*X/9.0D0+160.0D0*X/3.0D0
     6+208.0D0/X/9.0D0)*DLM+(-16.0D0+64.0D0*X*X/3.0D0-16.0D0*X
     7-32.0D0/X)*ZETA2+(16.0D0-16.0D0*X+64.0D0/X/3.0D0
     8+32.0D0*X*X/3.0D0)*S11+(-32.0D0-32.0D0*X*X/3.0D0-32.0D0
     9*X-32.0D0/X/3.0D0)*(S11M+DLX*DLP)+304.0D0/9.0D0+832.0D0
     1*X*X/9.0D0-1216.0D0*X/9.0D0+80.0D0/X/9.0D0
      C2Q1=CF*TF*(A1*DL2+A2*DL+A3)
      RETURN
      END

C Equation (4.7): Light quark coefficient function for FL
      DOUBLE PRECISION FUNCTION CLQ2(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      DL=DLOG(Q2/HM2)
      DLX=DLOG(X)
      DLM=DLOG(1.0D0-X)
      CLQ2=16.0D0*CF*TF*(X*DL+X*DLM-2.0D0*X*DLX-25.0D0*X
     1/6.0D0+1.0D0)/3.0D0
      RETURN
      END

C Equation (4.8): Light quark coefficient function for F2
C -- Regular part
      DOUBLE PRECISION FUNCTION C2Q2R(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      DLX=DLOG(X)
      DLM=DLOG(1.0D0-X)
      A3=(1.0D0+13.0D0*X)*DLM-(3.0D0+23.0D0*X)*DLX+29.0D0/6.0D0
     1-295.0D0*X/6.0D0
      C2Q2R=2.0D0*CF*TF*A3/3.0D0
      RETURN
      END

C Equation (4.8): Light quark coefficient function for F2
C -- Singular part
      DOUBLE PRECISION FUNCTION C2Q2S(X,Q2,HM2)
      IMPLICIT REAL*8(A-H,O-Z)
      DOUBLE PRECISION WGPLG
      PARAMETER (TF=0.5D0)
      PARAMETER (CF=4D0/3D0)
      ZETA2=1.6449340668D0
      SPX=WGPLG(1,1,1.0D0-X)
      DLX=DLOG(X)
      DLX2=DLX*DLX
      DLM=DLOG(1.0D0-X)
      DLM2=DLM*DLM
      DL=DLOG(Q2/HM2)
      DL2=DL*DL
      A1=2.0D0*(1.0D0+X*X)*DL2/(1.0D0-X)+(4.0D0*(1.0D0+X*X)*DLM/
     1(1.0D0-X)-8.0D0*(1.0D0+X*X)*DLX/(1.0D0-X)-29.0D0*(1.0D0+X*
     2X)/(1.0D0-X)/3.0D0+13.0D0*X+1.0D0)*DL
      A2=(1.0D0+X*X)*(-4.0D0*SPX-4.0D0*ZETA2+2.0D0*DLM2-8.0D0*DLX
     1*DLM-29.0D0*DLM/3.0D0+6.0D0*DLX2+67.0D0*DLX/3.0D0+359D0/18.0D0
     2)/(1.0D0-X)
      C2Q2S=2.0D0*CF*TF*(A1+A2)/3.0D0
      RETURN
      END
